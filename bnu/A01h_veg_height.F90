!simard.f
!Assign tree heights from Simard et al. (2011) to tree PFTs in 
! EntMM_lc_maxlai_#x#.bin generated by modis_entpftg.f.
!
! It does the same thing as BNU/A01*, but instead of assigning LAImax,
! it assigns height to the cover fractions.  Both LAImax and Height
! are 1 km scale assignments to the 1 km subgrid cover fractions (all
! subgrid fractions in a 1 km grid cell get the same LAImax and
! Height, except for shrubs and grasses, which get assigned some
! ballpark heights).
!
!-----------------------------------------------------------------
      
program simard

    use netcdf
    use chunker_mod
    use chunkparams_mod
    use paths_mod
    use ent_labels_mod
    use geom_mod

implicit none
      
    type(Chunker_t) :: chunker
    integer :: jchunk, ichunk    ! Index of current chunk
    integer :: jc, ic    ! Index WITHIN current chunk
    integer :: jj, ii            ! Index in full space

    ! ------ Inputs
    type(ChunkIO_t) :: io_sim    ! Simard heights
    type(ChunkIO_t), dimension(NENT20) :: io_lc
    ! ------ Outputs
    type(ChunkIO_t), dimension(NENT20) :: io_out
    type(ChunkIO_t) :: io_lchgt_checksum

    real*4 :: SHEIGHT, OHEIGHT
    type(FileInfo_t) :: info
    integer :: k

MAIN_PROGRAM_FILE='A01h_veg_heights'
call init_ent_labels

! -----------------------------------------------------
! -----------------------------------------------------

call chunker%init(IM1km, JM1km, IMH*2,JMH*2, 'forplot', 100, 120, 10)

! ----------------------------------------------------------------------
!     GET NC FILES IDs

! ------------- Inputs     
!     SIMARD HEIGHTS
call chunker%nc_open_gz(io_sim, &
    DATA_DIR, DATA_INPUT, &
    'height/', &
    'simard_forest_heights.nc', 'heights', 1)

!     ENTPFTLC
call chunker%nc_open_set(ent20, io_lc, &
    LAI_SOURCE, 'M', 'lc', 2004, 'ent17', '1.1')

! ---------------- Outputs
! ENTPFT heights

call chunker%nc_create_set( &
    ent20, io_out, lc_weights(io_lc, 1d0, 0d0), &
    LAI_SOURCE, 'M', 'hgt', 2004, 'ent17', '1.1')

! ---------- Checksums
call chunker%file_info(info, ent20, LAI_SOURCE, 'M', 'lchgt', 2004, 'ent17', '1.1', &
    varsuffix='_checksum')
call chunker%nc_create(io_lchgt_checksum, &
    weighting(chunker%wta1,1d0,0d0), &
    info%dir, info%leaf, info)


! Quit if we had any problems opening files
call chunker%nc_check(MAIN_PROGRAM_FILE)
#ifdef JUST_DEPENDENCIES
stop 0
#endif

! --------------------------------- main loop
! Use these loop bounds for testing...
! it chooses a land area in Asia

#ifdef ENTGVSD_DEBUG
do jchunk = dbj0,dbj1
do ichunk = dbi0,dbi1
#else
do jchunk = 1,chunker%nchunk(2)
do ichunk = 1,chunker%nchunk(1)
#endif

    call chunker%move_to(ichunk,jchunk)

    do jc = 1,chunker%chunk_size(2)
    do ic = 1,chunker%chunk_size(1)

        ! Compute overall NetCDF index of current cell
        ii = (ichunk-1)*chunker%chunk_size(1)+(ic-1)+1
        jj = (jchunk-1)*chunker%chunk_size(2)+(jc-1)+1

        io_lchgt_checksum%buf(ic,jc) = 0
        do k = 1,NENT20
            if (k == CV_WATER) then
                OHEIGHT = 0d0
            else
                OHEIGHT = FillValue   ! Default if nothing in this PFT
                if (io_lc(k)%buf(ic,jc) > 0) then
                    ! Lookup what the height should be
                    SHEIGHT = io_sim%buf(ic,jc)
                    OHEIGHT = SHEIGHT * heights_form(1,k) + heights_form(2,k)
                    io_lchgt_checksum%buf(ic,jc) = io_lchgt_checksum%buf(ic,jc) + &
                        io_lc(k)%buf(ic,jc) * OHEIGHT
                    ! TODO: Better way: inverse weighted average of
                    ! gridcell height, so tree heights are taller.  So
                    ! average gridcell height (averaging over non-zero
                    ! LC's) is same as the Simard height.
                end if
            end if

            io_out(k)%buf(ic,jc) = OHEIGHT
        end do    ! p

    end do    ! ic
    end do    ! jc
    
    call chunker%write_chunks

end do ! ichunk
end do ! jchunk

call chunker%close_chunks

      
end program simard

